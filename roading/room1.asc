// room script file

struct Vec3 {
  float x, y, z;
};

float turn_speed = 1.4;
float max_speed_forward = 4.0;
float max_speed_forward_road = 4.0;
float max_speed_forward_dirt = 2.0;
float max_speed_backward = 2.0;

ZoneManager zManager;

int timer, ground_frame;

// the little player ship
Mode7Object* o7car;
Mode7Object* o7player;

InputParams* hidCar;
InputParams* hidPlayer;

DynamicSprite* ground;
DynamicSprite* minimap;
int current_zone;
int zone_road = 2, zone_dirt = 4, zone_wall = 1;

float abs(float x) {
  if (x < 0.0) return -x;
  return x;
}

Mode7World m7;

bool in_car;

void togglePlayMode() {
  if(in_car) {
    //Display("Out of Car!");
    
    // place player on the side of driver door
    float sin_ortho = Maths.Sin(Maths.DegreesToRadians(o7car.Angle)-Maths.Pi/2.0);
    float cos_ortho = Maths.Cos(Maths.DegreesToRadians(o7car.Angle)-Maths.Pi/2.0);    
    o7player.X = o7car.X + sin_ortho * 20.0;
    o7player.Z = o7car.Z - cos_ortho * 20.0;
    
    m7.SetCamera(o7player.X, o7player.Y + 6.0, o7player.Z, 0.0, 0.0, 175.0);
  } else {
    
    m7.SetCamera(o7car.X, 25.0, o7car.Z, 0.0, 0.0, 175.0);
  }
  
  in_car = !in_car;  
}




void processPlayerShip() {
  // zone
  int w = 1024;
  int h = 1024;
  int x = FloatToInt(o7car.X, eRoundNearest) + w / 2;
  int y = FloatToInt(o7car.Z, eRoundNearest) + h / 2;
  current_zone = zone_road;
  if (x >= 0 && x < w && y >= 0 && y < h) current_zone = GetWalkableAreaAtRoom(x, y);
  if (current_zone == zone_dirt) {
    max_speed_forward = max_speed_forward_dirt;
    if (hidCar.CurrentSpeed != 0.0) {
      int step = FloatToInt(hidCar.MaxSpeedForward / abs(hidCar.CurrentSpeed), eRoundNearest) + 3;
      if (step != 0 && (timer % step) < step/2) o7car.Y = 0.5;
      else o7car.Y = 0.0;
    }
  }
  else {
    o7car.Y = 0.0;
    max_speed_forward = max_speed_forward_road;
  }
}  

void drawSplashes()
{
  if (hidCar.CurrentSpeed > 0.0 && current_zone == zone_dirt) {
    DrawingSurface* ds = m7.Screen.GetDrawingSurface();
    
    ds.DrawImage(m7.Objects[0].ScreenX, m7.Objects[0].ScreenY, 24, 0, m7.Objects[0].ScreenWidth, m7.Objects[0].ScreenHeight);
    ds.Release();
  }
}

void update_m7wobjs_from_zmanager()
{
  m7.RemoveAllsObjects();
  for(int i=0; i<zManager.ObjectCount; i++)
  {
    m7.AddExternalObject(zManager.Objects[i]);
  }
}

// room events
function room_Load()
{
  zManager.WorldWidth = 1024;
  zManager.WorldHeight = 1024;
  zManager.ZoneXYCount = 4;
  
  hidCar = InputParams.NewInputParams(1.4, 4.0, 4.0, 2.0, 2.0);
  
  hidPlayer = InputParams.NewInputParams(1.4, 4.0, 4.0, 2.0, 2.0);
  
  // show FPS
  Debug(4, 1);
    
  o7car = zManager.AddObject(372, 185, 0.36, 27);
  o7player = zManager.AddObject(372, 185, 0.36, 2);
  o7player.Visible = false;
  
  for(int i=0; i<256; i++)
  {
    int obj_x, obj_y;
    do {
      obj_x = Random(1024)-512;
      obj_y = Random(1024)-512;
    } while (GetWalkableAreaAtRoom(obj_x+512, obj_y+512) != 4);
    
    zManager.AddObject(obj_x, obj_y, 1.0, 9);
  }
  
  update_m7wobjs_from_zmanager();
  
  int zones_slot = 23;
  
  m7.SetViewscreen(0, 0, 320, 180);
  m7.SetBgColor(13823);
  m7.SetSkyColor(12255);
  
  m7.SetCamera(0.0, 25.0, 0.0, 0.0, 0.0, 175.0);
  togglePlayMode();
}



function room_AfterFadeIn() {
  
  DrawingSurface* surf = Room.GetDrawingSurfaceForBackground();
  ground = DynamicSprite.CreateFromDrawingSurface(surf, 0, 0, 1024, 1024);
  surf.Release();
  
  m7.SetGroundSprite(ground.Graphic);
  m7.SetHorizonSprite(64);
  
}

function room_RepExec()
{
  timer++;
  if (timer == 48) timer = 0;
  if (timer % 4 == 0) {
    minimap = m7.DrawWorld2D();
    minimap.Resize(gMiniMap.Width, gMiniMap.Height);
    gMiniMap.BackgroundGraphic = minimap.Graphic;
  }

 
  if(in_car) {
    input_car(o7car, hidCar);
    m7.TargetCamera(o7car.X, o7car.Y, o7car.Z, o7car.Angle, eCameraTarget_FollowBehind);
    zManager.UpdateCurrentPos(o7car.X, o7car.Z);
  } else {
    input_player(o7player, hidPlayer);
    m7.TargetCamera(o7player.X, o7player.Y, o7player.Z, o7player.Angle, eCameraTarget_FirstPerson);
    zManager.UpdateCurrentPos(o7player.X, o7player.Z);
  }
  
  if(zManager.ZoneChanged)
  {
    update_m7wobjs_from_zmanager();
  }
  
  m7.UpdateObjects();
  
  // determine kart sprite
  int kart_sprite = m7.GetAngleObjectAndCamera(0) / 22;
  if (kart_sprite <= 0) {
    kart_sprite = 27 - kart_sprite;
  } else {
    kart_sprite = 43 - kart_sprite;
  }        
  
  m7.Objects[0].Graphic = kart_sprite; 
 // m7.ResetGround();
  m7.Draw();
 // drawSplashes();
  m7.DrawObjects();
  
  gScreen.BackgroundGraphic = m7.Screen.Graphic;
}

void on_key_press(eKeyCode k) {
  m7.DebugKeyPress(k);
  
  if(k == eKeyC) {
    togglePlayMode();
  }
  
}

// Clouds: ansimuz - Sunnyland
// Water: zabin - The Battle for Wesnoth Water
// Ship: helianthus games - FREE pixel art Viking ship 16 directions